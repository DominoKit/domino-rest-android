apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'maven'

group = 'org.dominokit'
version = '1.0-rc.4-SNAPSHOT'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 24

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
        packagingOptions {
            exclude 'META-INF/gwt/*'
            exclude '**/*.gwt.xml'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url "https://repo.vertispan.com/gwt-snapshot"
    }
}

dependencies {
    api 'io.reactivex.rxjava2:rxjava:2.2.10'
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'cz.msebera.android:httpclient:4.5.8'
    api "org.dominokit:domino-rest-shared:1.0-rc.4-SNAPSHOT"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: System.getenv("SONATYPE_USERNAME"), password: System.getenv("SONATYPE_PASSWORD"))
                pom.groupId = group
                pom.artifactId = "domino-rest-android"
                pom.version = version
                pom.project {
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'Ahmad K. Bawaneh'
                            email = 'akabme@gmail.com'
                        }
                        developer {
                            name = 'Rafat J. Al-Barouki'
                            email = 'rafat.albarouki@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/DominoKit/domino-rest-android.git'
                        developerConnection = 'scm:git:git@github.com:DominoKit/domino-rest-android.git'
                        url = 'https://github.com/DominoKit/domino-rest-android'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'domino-rest-android'
            version = version

            from components.android
        }
    }
}